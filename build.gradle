// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        jcenter()
        mavenCentral()
        google()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.72"
        classpath 'com.google.gms:google-services:4.3.3'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.2.0'
        classpath 'gradle.plugin.org.mozilla.rust-android-gradle:plugin:0.8.3'
        classpath 'com.github.triplet.gradle:play-publisher:2.8.0'
    }
}

static String detectNdkVersion() {

    def version = "21.3.6528147"

    def androidHome = System.getenv("ANDROID_HOME")

    if (androidHome == null) return version

    def versionFile = new File(androidHome,"ndk-bundle/source.properties")

    if (!versionFile.isFile()) return version

    def versionProperties = new Properties()

    versionProperties.load(versionFile.newDataInputStream())

    return versionProperties.getProperty("Pkg.Revision",version)

}

ext {

    ndkVersion = detectNdkVersion()

}

allprojects {
    repositories {
        // The order in which you list these repositories matter.
        jcenter()
        mavenCentral()
        google()
    }
}